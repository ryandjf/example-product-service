plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "checkstyle"
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.github.spotbugs" version "3.0.0"
    id "com.gorylenko.gradle-git-properties" version "2.2.0"
    id "com.diffplug.gradle.spotless" version "3.26.1"
    id "org.owasp.dependencycheck" version "5.2.4"
    id "com.palantir.docker" version "0.22.1"
    id "com.google.cloud.tools.jib" version "1.8.0"
    id "org.sonarqube" version "2.8"
    id "com.cinnober.gradle.semver-git" version "2.5.0"
}

repositories {
    maven {
        url "https://maven.aliyun.com/repository/central"
    }
    mavenCentral()
}

apply from: "${rootProject.projectDir}/gradle/integration-test.gradle"
apply from: "${rootProject.projectDir}/gradle/idea.gradle"
apply from: "${rootProject.projectDir}/gradle/jacoco.gradle"
apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
apply from: "${rootProject.projectDir}/gradle/spotbugs.gradle"
apply from: "${rootProject.projectDir}/gradle/git-hooks.gradle"
apply from: "${rootProject.projectDir}/gradle/spotless.gradle"
apply from: "${rootProject.projectDir}/gradle/dependency-check.gradle"
apply from: "${rootProject.projectDir}/gradle/sonarqube.gradle"

group = 'net.thoughtworks'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.modelmapper:modelmapper:2.3.5')
    implementation('org.flywaydb:flyway-core:6.1.3')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation("io.rest-assured:spring-mock-mvc:4.1.2")
    testImplementation("io.rest-assured:json-path:4.1.2")
    testImplementation("io.rest-assured:xml-path:4.1.2")
    testImplementation('com.tngtech.archunit:archunit:0.12.0')
    testImplementation('com.tngtech.archunit:archunit-junit4:0.12.0')

    runtimeOnly('mysql:mysql-connector-java:8.0.18')
}

jib {
    from {
        image = 'openjdk:8-jdk-slim'
    }
    to {
        image = "${group}/${jar.archiveBaseName}"
    }
    container {
        mainClass = 'net.thoughtworks.ProductServiceApplication'
        jvmFlags = ['-Dserver.port=9090']
        ports = ['9090']
        useCurrentTimestamp = true
    }
}

wrapper {
    gradleVersion = '6.0.1'
}
