plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "checkstyle"
    id "org.springframework.boot" version "2.4.4"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.github.spotbugs" version "4.7.0"
    id "com.gorylenko.gradle-git-properties" version "2.3.0"
    id 'com.diffplug.spotless' version '5.11.1'
    id "org.owasp.dependencycheck" version "6.1.5"
    id 'org.sonatype.gradle.plugins.scan' version '2.0.6'
    id "com.palantir.docker" version "0.26.0"
    id "com.google.cloud.tools.jib" version "2.8.0"
    id "org.sonarqube" version "3.1.1"
    id "com.cinnober.gradle.semver-git" version "3.0.0"
}

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/spring/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/spring-plugin/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/gradle-plugin/'
    }
    mavenLocal()
    mavenCentral()
}

apply from: "${rootProject.projectDir}/gradle/integration-test.gradle"
apply from: "${rootProject.projectDir}/gradle/idea.gradle"
apply from: "${rootProject.projectDir}/gradle/jacoco.gradle"
apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
apply from: "${rootProject.projectDir}/gradle/spotbugs.gradle"
apply from: "${rootProject.projectDir}/gradle/git-hooks.gradle"
apply from: "${rootProject.projectDir}/gradle/spotless.gradle"
apply from: "${rootProject.projectDir}/gradle/dependency-check.gradle"
apply from: "${rootProject.projectDir}/gradle/sonarqube.gradle"
apply from: "${rootProject.projectDir}/gradle/sonatype-oss-index.gradle"

group = 'net.thoughtworks'
version = '0.3.0'
sourceCompatibility = '15'

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.modelmapper:modelmapper:2.4.0')
    implementation('org.flywaydb:flyway-core:7.7.2')
    implementation('org.fluentd:fluent-logger:0.3.4')
    implementation('com.sndyuk:logback-more-appenders:1.8.4')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.rest-assured:spring-mock-mvc:4.3.3')
    testImplementation('io.rest-assured:json-path:4.3.3')
    testImplementation('io.rest-assured:xml-path:4.3.3')
    testImplementation('com.tngtech.archunit:archunit:0.17.0')
    testImplementation('com.tngtech.archunit:archunit-junit4:0.17.0')

    runtimeOnly('mysql:mysql-connector-java:8.0.23')
}

bootJar {
    layered()
}

jib {
    from {
        image = 'openjdk:15-jre-slim'
    }
    to {
        image = "${group}/${jar.archiveBaseName}"
        tags = ["${version}", 'latest']
    }
    container {
        ports = ['8080', '8081']
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}

wrapper {
    gradleVersion = '6.8.1'
}
